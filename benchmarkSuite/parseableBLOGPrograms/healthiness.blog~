/** 
 * healthiness model
 * This BLOG program describes the model for healthiness in 
 * a person.
 * See exercise 3.5 in:
 *   Probabilistic Graphical Models: Principles and Techniques
 *   Daphne Koller, Nir Friedman, MIT 2009
 * Each person is described by whether they are health
 * conscious, have free time, exercise, have a good diet, 
 * have normal weight, have high cholesterol, and whether
 * they tested positive for high cholesterol.
 * @author: jnieh
 * @date: 2012-09-13
 */

random Boolean HealthConscious ~ BooleanDistrib(0.5);
random Boolean LittleFreeTime ~ BooleanDistrib(0.5);
random Boolean Exercise
  ~ case [HealthConscious, LittleFreeTime] in
               {[true, true] -> BooleanDistrib(0.5),
                [true, false] -> BooleanDistrib(0.9),
                [false, true] -> BooleanDistrib(0.1),
                [false, false] -> BooleanDistrib(0.5)}
    ;
random Boolean GoodDiet
  ~ case HealthConscious in {true -> BooleanDistrib(0.7),
                false -> BooleanDistrib(0.3)};
random Boolean NormalWeight 
  ~ case [GoodDiet, Exercise] in 
               {[true, true] -> BooleanDistrib(0.8),
                [true, false] -> BooleanDistrib(0.5),
                [false, true] -> BooleanDistrib(0.5),
                [false, false] -> BooleanDistrib(0.2)}
    ;
random Boolean HighCholesterol
  ~ case GoodDiet in 
                {true -> BooleanDistrib(0.3),
                 false -> BooleanDistrib(0.7)}
    ;
random Boolean TestedHighCholesterol
  ~ case HighCholesterol in {true -> BooleanDistrib(0.9),
               false -> BooleanDistrib(0.1)}
    ;
